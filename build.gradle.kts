import org.jetbrains.kotlin.gradle.tasks.KotlinCompile


plugins {
	val kotlinVersion = "1.9.23"

	id("org.springframework.boot") version "3.2.3"
	id("io.spring.dependency-management") version "1.1.4"
	id("com.github.davidmc24.gradle.plugin.avro") version "1.9.1"
	kotlin("plugin.serialization") version kotlinVersion
	kotlin("jvm") version kotlinVersion
	kotlin("plugin.spring") version kotlinVersion
}

group = "no.nav.amt-person-service"
version = "0.0.1-SNAPSHOT"
java.sourceCompatibility = JavaVersion.VERSION_21

repositories {
	mavenCentral()
	maven { setUrl("https://github-package-registry-mirror.gc.nav.no/cached/maven-release") }
	maven { setUrl("https://packages.confluent.io/maven/") }
}

val commonVersion = "3.2024.02.21_11.18-8f9b43befae1"
val okhttp3Version = "4.12.0"
val kotestVersion = "5.8.1"
val poaoTilgangVersion = "2024.03.14_08.12-3b060a35fac7"
val testcontainersVersion = "1.19.7"
val tokenSupportVersion = "4.1.3"
val mockkVersion = "1.13.10"
val lang3Version = "3.14.0"
val shedlockVersion = "5.12.0"
val confluentVersion = "7.5.1"
val avroVersion = "1.11.3"
val jacksonVersion = "2.17.0"
val micrometerVersion = "1.12.4"
val mockOauth2ServerVersion = "2.1.2"

extra["postgresql.version"] = "42.7.2"

dependencies {
	implementation("org.springframework.boot:spring-boot-starter")
	implementation("org.springframework.boot:spring-boot-starter-actuator")
	implementation("org.springframework.boot:spring-boot-configuration-processor")
	implementation("org.springframework.boot:spring-boot-starter-validation")
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.springframework.boot:spring-boot-starter-logging")
	implementation("org.springframework.boot:spring-boot-starter-data-jdbc")

	implementation("org.springframework.retry:spring-retry")
	implementation("org.springframework:spring-aspects")

	implementation("org.jetbrains.kotlin:kotlin-reflect")
	implementation("org.jetbrains.kotlin:kotlin-stdlib")
	implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion")
	implementation("com.squareup.okhttp3:okhttp:$okhttp3Version")
	implementation("org.flywaydb:flyway-core")
	implementation("org.postgresql:postgresql")

	implementation("io.micrometer:micrometer-registry-prometheus:$micrometerVersion")
	implementation("no.nav.common:log:$commonVersion")
	implementation("no.nav.common:token-client:$commonVersion")
	implementation("no.nav.common:rest:$commonVersion")
	implementation("no.nav.common:job:$commonVersion")
	implementation("no.nav.common:kafka:$commonVersion")
	implementation("org.xerial.snappy:snappy-java:1.1.10.5") // overstyrer s√•rbar dependency

	implementation("io.confluent:kafka-avro-serializer:$confluentVersion")
	implementation("org.apache.avro:avro:$avroVersion")

	implementation("no.nav.poao-tilgang:client:$poaoTilgangVersion")

	implementation("org.apache.commons:commons-lang3:$lang3Version")

	implementation("no.nav.security:token-validation-spring:$tokenSupportVersion")

	implementation("net.javacrumbs.shedlock:shedlock-spring:$shedlockVersion")
	implementation("net.javacrumbs.shedlock:shedlock-provider-jdbc-template:$shedlockVersion")

	annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

	testImplementation("org.springframework.boot:spring-boot-starter-test")
	testImplementation("io.kotest:kotest-assertions-core-jvm:$kotestVersion")
	testImplementation("io.kotest:kotest-assertions-json-jvm:$kotestVersion")
	testImplementation("com.squareup.okhttp3:mockwebserver:$okhttp3Version")
	testImplementation("org.testcontainers:testcontainers:$testcontainersVersion")
	testImplementation("org.testcontainers:postgresql:$testcontainersVersion")
	testImplementation("org.testcontainers:kafka:$testcontainersVersion")
	testImplementation("io.mockk:mockk:$mockkVersion")
	testImplementation("no.nav.security:mock-oauth2-server:$mockOauth2ServerVersion")}

tasks.getByName<org.springframework.boot.gradle.tasks.bundling.BootJar>("bootJar") {
	this.archiveFileName.set("${archiveBaseName.get()}.${archiveExtension.get()}")
}

tasks.withType<KotlinCompile> {
	kotlinOptions {
		freeCompilerArgs = listOf("-Xjsr305=strict")
		jvmTarget = "21"
	}
}

tasks.withType<Test> {
	useJUnitPlatform()
}
